#Использовать "../../core"

Перем ВерсииOneScript;

Процедура ОписаниеКоманды(КомандаПриложения) Экспорт
	
	КомандаПриложения.Опция("remote r", Ложь, "Вывести список доступных к установке версий").Флаговый();
	КомандаПриложения.Опция("quiet q", Ложь, "Тихий режим, вывод только алиасов версий");
	КомандаПриложения.Опция("all a", Ложь, "Вывести список всех версий: установленных и доступных на сайте");

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	
	ВыводитьСписокВерсийНаСайте = КомандаПриложения.ЗначениеОпции("--remote");
	ВыводитьВсеВерсии = КомандаПриложения.ЗначениеОпции("--all");
	ТихийРежим = КомандаПриложения.ЗначениеОпции("--quiet");
	
	Если ВыводитьВсеВерсии Тогда
		ВывестиВсеВерсии(ТихийРежим);
	ИначеЕсли ВыводитьСписокВерсийНаСайте Тогда
		ВывестиСписокДоступныхКУстановкеВерсий(ТихийРежим);
	Иначе
		ВывестиСписокУстановленныхВерсий(ТихийРежим);
	КонецЕсли;

КонецПроцедуры

Процедура ВывестиВсеВерсии(Знач ТихийРежим)

	СписокУстановленныхВерсий = ВерсииOneScript.ПолучитьСписокУстановленныхВерсий();
	СписокДоступныхВерсий = ВерсииOneScript.ПолучитьСписокДоступныхКУстановкеВерсий();
	
	ВсеВерсии = Новый ТаблицаЗначений;
	ВсеВерсии.Колонки.Добавить("Алиас", Новый ОписаниеТипов("Строка"));
	ВсеВерсии.Колонки.Добавить("Версия", Новый ОписаниеТипов("Строка"));
	ВсеВерсии.Колонки.Добавить("ПутьЛокальный", Новый ОписаниеТипов("Строка"));
	ВсеВерсии.Колонки.Добавить("ПутьСервер", Новый ОписаниеТипов("Строка"));
	ВсеВерсии.Колонки.Добавить("ЭтоСимлинк", Новый ОписаниеТипов("Булево"));

	Для Каждого ДоступнаяВерсия Из СписокДоступныхВерсий Цикл		
		СтрокаВсеВерсии = ВсеВерсии.Найти(ДоступнаяВерсия.Алиас, "Алиас");
		Если СтрокаВсеВерсии = Неопределено Тогда
			СтрокаВсеВерсии = ВсеВерсии.Добавить();
			СтрокаВсеВерсии.Алиас = ДоступнаяВерсия.Алиас;
			СтрокаВсеВерсии.ЭтоСимлинк = Ложь;	
		КонецЕсли;
		
		СтрокаВсеВерсии.ПутьСервер = ДоступнаяВерсия.Путь;
	КонецЦикла;

	Для Каждого УстановленнаяВерсия Из СписокУстановленныхВерсий Цикл	
		СтрокаВсеВерсии = ВсеВерсии.Найти(УстановленнаяВерсия.Алиас, "Алиас");
		Если СтрокаВсеВерсии = Неопределено Тогда
			СтрокаВсеВерсии = ВсеВерсии.Добавить();
			СтрокаВсеВерсии.Алиас = УстановленнаяВерсия.Алиас;
			СтрокаВсеВерсии.ЭтоСимлинк = УстановленнаяВерсия.ЭтоСимлинк;	
		КонецЕсли;
		
		СтрокаВсеВерсии.Версия = УстановленнаяВерсия.Версия;
		СтрокаВсеВерсии.ПутьЛокальный = УстановленнаяВерсия.Путь;
	КонецЦикла;
	
	ВсеВерсии.Сортировать("Алиас");
	
	Для Каждого СтрокаВерсия Из ВсеВерсии Цикл
		
		Если ТихийРежим И СтрокаВерсия.ЭтоСимлинк Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТихийРежим Тогда
			Сообщение = СтрокаВерсия.Алиас;
		Иначе	
			Сообщение = СтрШаблон(
				"%1 -> %2 -> %3 -> %4",
				СтрокаВерсия.Алиас,
				?(СтрокаВерсия.Версия = "", "unknown", СтрокаВерсия.Версия),
				?(СтрокаВерсия.ПутьЛокальный = "", "not installed", СтрокаВерсия.ПутьЛокальный),
				?(СтрокаВерсия.ПутьСервер = "", "unknown", СтрокаВерсия.ПутьСервер)
			);
		КонецЕсли;

		УстанавливаемыйСтатусСообщения = ?(СтрокаВерсия.Алиас = "current", СтатусСообщения.Информация, СтатусСообщения.БезСтатуса);
		Сообщить(Сообщение, УстанавливаемыйСтатусСообщения);

	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиСписокУстановленныхВерсий(Знач ТихийРежим)
	СписокУстановленныхВерсий = ВерсииOneScript.ПолучитьСписокУстановленныхВерсий();
	Для Каждого УстановленнаяВерсия Из СписокУстановленныхВерсий Цикл
		
		Если ТихийРежим И УстановленнаяВерсия.ЭтоСимлинк Тогда
			Продолжить;
		КонецЕсли;

		Если ТихийРежим Тогда
			Сообщение = УстановленнаяВерсия.Алиас;
		Иначе
			Сообщение = СтрШаблон("%1 -> %2 (%3)", УстановленнаяВерсия.Алиас, УстановленнаяВерсия.Версия, УстановленнаяВерсия.Путь);
		КонецЕсли;	
		УстанавливаемыйСтатусСообщения = ?(УстановленнаяВерсия.Алиас = "current", СтатусСообщения.Информация, СтатусСообщения.БезСтатуса);
		Сообщить(Сообщение, УстанавливаемыйСтатусСообщения);
	КонецЦикла;
КонецПроцедуры

Процедура ВывестиСписокДоступныхКУстановкеВерсий(Знач ТихийРежим)
	
	СписокДоступныВерсий = ВерсииOneScript.ПолучитьСписокДоступныхКУстановкеВерсий();
	Для Каждого ДоступнаяВерсия Из СписокДоступныВерсий Цикл
		Если ТихийРежим Тогда
			Сообщение = ДоступнаяВерсия.Алиас;
		Иначе
			Сообщение = СтрШаблон("%1 (%2)", ДоступнаяВерсия.Алиас, ДоступнаяВерсия.Путь);
		КонецЕсли;	
		Сообщить(Сообщение);
	КонецЦикла;

КонецПроцедуры

ВерсииOneScript = Новый ВерсииOneScript();