#Использовать fs

Перем ЭтоWindows;

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Аргумент("VERSION", , "Используемая версия OneScript. Допустимо использовать трехномерные версии (1.0.17, 1.0.18), latest, stable, dev, night-build").ТСтрока();

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	ИспользуемаяВерсия = Команда.ЗначениеАргумента("VERSION");
	ИспользоватьВерсиюOneScript(ИспользуемаяВерсия);
КонецПроцедуры

Процедура ИспользоватьВерсиюOneScript(Знач ИспользуемаяВерсия) Экспорт
	
	ПроверитьНаличиеИспользуемойВерсии(ИспользуемаяВерсия);

	КаталогУстановки = ПараметрыПриложения.КаталогУстановкиПоУмолчанию();
	КаталогУстановкиВерсии = ОбъединитьПути(КаталогУстановки, ИспользуемаяВерсия);
		
	ПутьКОбщемуКаталогуOneScript = ОбъединитьПути(КаталогУстановки, "current");
	УдалитьФайлы(ПутьКОбщемуКаталогуOneScript);

	СоздатьСимЛинкНаКаталог(ПутьКОбщемуКаталогуOneScript, КаталогУстановкиВерсии);
	ДобавитьКаталогBinВPath(ОбъединитьПути(ПутьКОбщемуКаталогуOneScript, "bin"));

КонецПроцедуры

Процедура СоздатьСимЛинкНаКаталог(Знач Ссылка, Знач ПутьНазначения)
	
	Если ЭтоWindows Тогда
		Команда = Новый Команда;
		Команда.УстановитьКоманду("mklink");
		Команда.ДобавитьПараметр("/D");
		Команда.ДобавитьПараметр(Ссылка);
		Команда.ДобавитьПараметр(ПутьНазначения);

		Команда.Исполнить();
	Иначе
		Команда = Новый Команда;
		Команда.УстановитьКоманду("ln");
		Команда.ДобавитьПараметр("-s");
		Команда.ДобавитьПараметр(ПутьНазначения);
		Команда.ДобавитьПараметр(Ссылка);
		
		Команда.Исполнить();
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьКаталогBinВPath(Знач ПутьККаталогуBin)
	
	ПеременнаяPATH = ПолучитьПеременнуюСреды("PATH");
	Если СтрНайти(ПеременнаяPATH, ПутьККаталогуBin) <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПеременнуюСреды("PATH", ПеременнаяPATH + ";" + ПутьККаталогуBin, РасположениеПеременнойСреды.Пользователь);

КонецПроцедуры

Процедура ПроверитьНаличиеИспользуемойВерсии(Знач ИспользуемаяВерсия)
	
	КаталогУстановки = ПараметрыПриложения.КаталогУстановкиПоУмолчанию();
	КаталогУстановкиВерсии = ОбъединитьПути(КаталогУстановки, ИспользуемаяВерсия);
	Если НЕ ФС.КаталогСуществует(КаталогУстановкиВерсии) Тогда
		ВызватьИсключение СтрШаблон("Не обнаружена требуемая версия <%1>", ИспользуемаяВерсия);
	КонецЕсли;

КонецПроцедуры

СистемнаяИнформация = Новый СистемнаяИнформация;
ЭтоWindows = Найти(ВРег(СистемнаяИнформация.ВерсияОС), "WINDOWS") > 0;
