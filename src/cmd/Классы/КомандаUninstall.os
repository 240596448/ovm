Перем ВерсииOneScript;

Процедура ОписаниеКоманды(КомандаПриложения) Экспорт
	
	КомандаПриложения.Опция("force f", Ложь, "Удалять даже текущую используемую версию");
	КомандаПриложения.Аргумент("VERSION", , "Удаляемая версия OneScript (возвможна передача нескольких значений). Допустимо использовать трехномерные версии (1.0.17, 1.0.18), latest, stable, dev, night-build")
		.ТМассивСтрок();
		
	КомандаПриложения.Спек = "[--force] VERSION...";
КонецПроцедуры

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	
	МассивВерсийКУдалению = КомандаПриложения.ЗначениеАргумента("VERSION");
	УдалятьТекущуюВерсию = КомандаПриложения.ЗначениеОпции("--force");

	ДеинсталляторOneScript = Новый ДеинсталляторOneScript();
	
	Для Каждого ВерсияКУдалению Из МассивВерсийКУдалению Цикл
		Если ВерсииOneScript.ЭтоТекущаяВерсия(ВерсияКУдалению) И НЕ УдалятьТекущуюВерсию Тогда
			Сообщение = СтрШаблон("Версия <%1> не удалена, т.к. является текущей.", ВерсияКУдалению);
			Сообщить(Сообщение);
			Продолжить;
		КонецЕсли;
		ДеинсталляторOneScript.УдалитьOneScript(ВерсияКУдалению);
	КонецЦикла;
	
КонецПроцедуры

ВерсииOneScript = Новый ВерсииOneScript();
