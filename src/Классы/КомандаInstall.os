#Использовать 1commands
#Использовать fs
#Использовать tempfiles

Перем ЭтоWindows;

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	ВерсияКУстановке = Команда.ЗначениеАргумента("VERSION");
	УстановитьOneScript(ВерсияКУстановке);
КонецПроцедуры

Процедура УстановитьOneScript(Знач ВерсияКУстановке)
	
	КаталогУстановки = ПараметрыПриложения.КаталогУстановкиПоУмолчанию();
	КаталогУстановкиВерсии = ОбъединитьПути(КаталогУстановки, ВерсияКУстановке);
	ФС.ОбеспечитьКаталог(КаталогУстановки);
	ФС.ОбеспечитьПустойКаталог(КаталогУстановкиВерсии);
	
	ФайлУстановщика = СкачатьФайлУстановщика(ВерсияКУстановке);
	
	УстановитьOneScriptИзZipАрхива(ФайлУстановщика, КаталогУстановкиВерсии);
	
	КомандаUse = Новый КомандаUse();
	КомандаUse.ИспользоватьВерсиюOneScript(ВерсияКУстановке);

КонецПроцедуры

Функция СкачатьФайлУстановщика(Знач ВерсияКУстановке)
	
	ПутьКСохраняемомуФайлу = ВременныеФайлы.НовоеИмяФайла("zip");
	
	Ресурс = СтрШаблон("downloads/%1/OneScript-%2.zip", СтрЗаменить(ВерсияКУстановке, ".", "_"), ВерсияКУстановке);
	Соединение = Новый HTTPСоединение("http://oscript.io");
	Запрос = Новый HTTPЗапрос(Ресурс);
	
	Ответ = Соединение.Получить(Запрос, ПутьКСохраняемомуФайлу);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.КодСостояния;
	КонецЕсли;
	
	Возврат ПутьКСохраняемомуФайлу;
	
КонецФункции

Процедура УстановитьOneScriptИзZipАрхива(Знач ПутьКФайлуУстановщика, Знач КаталогУстановкиВерсии);
	
	ЧтениеZIPФайла = Новый ЧтениеZipФайла(ПутьКФайлуУстановщика);
	ЧтениеZIPФайла.ИзвлечьВсе(КаталогУстановкиВерсии);
	ЧтениеZIPФайла.Закрыть();
	
КонецПроцедуры

СистемнаяИнформация = Новый СистемнаяИнформация;
ЭтоWindows = Найти(ВРег(СистемнаяИнформация.ВерсияОС), "WINDOWS") > 0;
	