Перем ЭтоWindows;

Процедура ВыполнитьКоманду(Знач Команда) Экспорт
	ИспользуемаяВерсия = Команда.ЗначениеАргумента("VERSION");
	ИспользоватьВерсиюOneScript(ИспользуемаяВерсия);
КонецПроцедуры

Процедура ИспользоватьВерсиюOneScript(Знач ИспользуемаяВерсия) Экспорт
	
	КаталогУстановки = ПараметрыПриложения.КаталогУстановкиПоУмолчанию();
	КаталогУстановкиВерсии = ОбъединитьПути(КаталогУстановки, ИспользуемаяВерсия);
		
	ПутьКОбщемуКаталогуOneScript = ОбъединитьПути(КаталогУстановки, "current");
	УдалитьФайлы(ПутьКОбщемуКаталогуOneScript);

	СоздатьСимЛинкНаКаталог(ПутьКОбщемуКаталогуOneScript, КаталогУстановкиВерсии);
	ДобавитьКаталогBinВPath(ОбъединитьПути(ПутьКОбщемуКаталогуOneScript, "bin"));

КонецПроцедуры

Процедура СоздатьСимЛинкНаКаталог(Знач Ссылка, Знач ПутьНазначения)
	
	Если ЭтоWindows Тогда
		Команда = Новый Команда;
		Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
		Команда.УстановитьКоманду("cmd");
		Команда.ДобавитьПараметр("/C");
		Команда.ДобавитьПараметр("mklink");
		Команда.ДобавитьПараметр("/D");
		Команда.ДобавитьПараметр(Ссылка);
		Команда.ДобавитьПараметр(ПутьНазначения);

		Команда.Исполнить();
	Иначе
		ВызватьИсключение "Не реализовано";	
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьКаталогBinВPath(Знач ПутьККаталогуBin)
	
	ПеременнаяPATH = ПолучитьПеременнуюСреды("PATH");
	Если СтрНайти(ПеременнаяPATH, ПутьККаталогуBin) <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоWindows Тогда
		Команда = Новый Команда;
		Команда.УстановитьКоманду("setx");
		Команда.ДобавитьПараметр("PATH=%PATH%;" + ПутьККаталогуBin);

		Команда.Исполнить();
	Иначе
		ВызватьИсключение "Не реализовано";
	КонецЕсли;

КонецПроцедуры

СистемнаяИнформация = Новый СистемнаяИнформация;
ЭтоWindows = Найти(ВРег(СистемнаяИнформация.ВерсияОС), "WINDOWS") > 0;
